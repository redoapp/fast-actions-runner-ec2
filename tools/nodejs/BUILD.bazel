load("@better_rules_javascript//commonjs:rules.bzl", "cjs_root")
load("@better_rules_javascript//nodejs:rules.bzl", "nodejs_install", "nodejs_modules_package")
load("@better_rules_javascript//typescript:rules.bzl", "js_import_ts")
load("//bazel/rules:rules.bzl", "query_bzl")
load("//tools/npm:npm.bzl", NPM_ROOTS = "ROOTS")
load(":roots.bzl", ROOTS = "TARGETS")

package(default_visibility = ["//visibility:public"])

NPM_ROOTS = [root for root in NPM_ROOTS if root["name"]]

query_bzl(
    name = "roots",
    out = "roots.bzl",
    query = "kind('cjs_root', //...)",
)

nodejs_install(
    name = "install",
    src = ":node_modules",
)

nodejs_modules_package(
    name = "node_modules",
    links = [target for target in ROOTS if target not in ("//:root", "//javascript/temporal-types:root")],
    deps = [":temporal_types"] + [":npm_%s" % dep["name"] for dep in NPM_ROOTS],
)

cjs_root(
    name = "root",
    package_name = "@redotech/tools-nodejs",
    descriptors = ["package.json"],
)

js_import_ts(
    name = "temporal_types",
    dep = "//javascript/temporal-types:lib",
)

[
    js_import_ts(
        name = "npm_%s" % dep["name"],
        dep = "@npm//%s:lib" % dep["name"],
    )
    for dep in NPM_ROOTS
]
